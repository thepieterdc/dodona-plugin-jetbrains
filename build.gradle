plugins {
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
}

group 'io.github.thepieterdc.dodona'
version '2.0.0'

sourceCompatibility = 1.8

buildConfig {
    packageName 'io.github.thepieterdc.dodona.plugin'
}

repositories {
    mavenCentral()

    maven {
        url "https://dl.bintray.com/thepieterdc/dodona-api-java"
    }

    maven {
        url "https://dl.bintray.com/thepieterdc/random-java"
    }
}

dependencies {
    compile group: 'io.github.thepieterdc.dodona', name: 'dodona-api-interface', version: '1.6.0'
    runtime group: 'io.github.thepieterdc.dodona', name: 'dodona-api-impl', version: '1.6.0'

    compile group: 'com.github.marlonlom', name: 'timeago', version: '4.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
    testCompile group: 'io.github.thepieterdc.random', name: 'random', version: '0.3.0'
}

intellij {
    downloadSources false
    pluginName 'dodona'
    plugins = ['java', 'PythonCore:2019.2.192.5728.98']
    version 'IU-192.5728.98'
}

jacocoTestReport {
    dependsOn test

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

patchPluginXml {
    changeNotes """
      <b>This version features a complete rewrite of the plugin.</b>
      
      <h3>New features</h3>
      <ul>
        <li>Project wizard: Add Dodona support to new projects.</li>
        <li>Submissions: You will now get a confirmation box if you attempt to submit a solution that contains syntax errors.</li>
        <li>ToolWindow: List upcoming deadlines for this course.</li>
        <li>ToolWindow: List previous submissions for the current opened exercise.</li>
      </ul>
      
      <h3>Improvements</h3>
      <ul>
        <li>Authentication should work smoother now.</li>
        <li>General performance improvements.</li>
        <li>Other servers besides dodona.ugent.be are now supported.</li>
      </ul>
      """

    pluginDescription "Companion plugin for the Ghent University Dodona platform, which allows you to submit exercises right from your favourite JetBrains IDE."

    sinceBuild '171.0'
}