import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.TestFrameworkType
import org.jetbrains.intellij.platform.gradle.models.ProductRelease
import groovy.json.JsonSlurper

plugins {
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.10.5'
}

group 'io.github.thepieterdc.dodona'
version '2.2.3-SNAPSHOT'

final String sinceBuild = '242.24807.4'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    intellijPlatform {
        defaultRepositories()
    }

    mavenCentral()

    maven {
        url = "https://maven.pkg.github.com/thepieterdc/dodona-api-java"
        credentials {
            username = "PublicToken"
            password = "\u0064\u0064\u0066\u0039\u0036\u0065\u0030\u0063\u0061\u0032\u0066\u0065\u0032\u0039\u0033\u0033\u0036\u0030\u0035\u0062\u0031\u0033\u0031\u0035\u0038\u0063\u0061\u0064\u0061\u0033\u0030\u0031\u0030\u0035\u0062\u0037\u0035\u0032\u0066\u0061"
        }
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2024.2.5'

        testFramework TestFrameworkType.JUnit5.INSTANCE

        bundledPlugin 'com.intellij.java'
        plugin 'PythonCore', '242.24807.4'
    }

    implementation group: 'io.github.thepieterdc.dodona', name: 'api', version: '2.0.8'
    implementation group: 'io.github.thepieterdc.dodona', name: 'impl', version: '2.0.8'

    implementation group: 'com.github.marlonlom', name: 'timeago', version: '4.2.0'

    testImplementation platform("org.junit:junit-bom:6.0.2")

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '6.0.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.21.0'
    testImplementation group: 'io.github.thepieterdc.random', name: 'random', version: '1.0.5'
}

tasks.register('calculateNextVersion') {
    doLast {
        def version = (project.version as String).split('\\.').collect { Integer.parseInt(it) }
        println(String.format("%s.%s.%s", version[0], version[1], version[2] + 1))
    }
}

tasks.register('generateBuildConfig') {
    var outputDir = file("$projectDir/src/main/java/")
    doFirst {
        def srcFile = new File(outputDir, "io/github/thepieterdc/dodona/plugin/BuildConfig.java")
        srcFile.parentFile.mkdirs()
        srcFile.write("""
package io.github.thepieterdc.dodona.plugin;
public class BuildConfig {
    public static final String VERSION = "$project.version";
}
""")
    }

    group = "build"
    outputs.upToDateWhen { false }
}

compileJava.dependsOn generateBuildConfig
prepareKotlinBuildScriptModel.dependsOn generateBuildConfig

intellijPlatform {
    pluginConfiguration {
        changeNotes = """
            - [BUGFIX] Cannot create new exercise with boilerplate code that contains \r\n newlines.
        """.stripIndent()
        description = 'Companion plugin for the Ghent University Dodona platform, which allows you to submit exercises right from your favourite JetBrains IDE. More information can be found at <a href="https://docs.dodona.be/en/guides/pycharm-plugin/">https://docs.dodona.be/en/guides/pycharm-plugin/</a>'
        id = "be.ugent.piedcler.dodona"
        ideaVersion {
            sinceBuild
            untilBuild = provider { null }
        }
        name = "dodona"
        vendor {
            email = "pieterdeclercq@outlook.com"
            name = "Pieter De Clercq"
            url = "https://thepieterdc.github.io/dodona"
        }
        version = project.version
    }

    pluginVerification {
        ides {
            ide IntelliJPlatformType.IntellijIdeaCommunity, getSinceBuild("IIC", sinceBuild)
            ide IntelliJPlatformType.IntellijIdeaCommunity, getLatestEAPBuild("IIC")
            ide IntelliJPlatformType.PyCharmCommunity, getSinceBuild("PCC", sinceBuild)
            ide IntelliJPlatformType.PyCharmCommunity, getLatestEAPBuild("PCC")
            }
    }

    publishing {
        channels = [(project.version as String).contains("SNAPSHOT") ? "nightly" : "stable"]
        token = System.getenv("JETBRAINS_MARKETPLACE_TOKEN")
    }
}

jacocoTestReport {
    dependsOn test

    reports {
        html.required = true
        xml.required = true
    }
}

wrapper {
    gradleVersion = '9.2.1'
}

static def getLatestEAPBuild(String product) {
    // Get the list of all versions.
    def versionUrl = new URI("https://data.services.jetbrains.com/products?code=" + product + "&fields=code%2Creleases.build%2Creleases.type").toURL()
    def parsedJson = new JsonSlurper().parse(versionUrl)

    // Find the latest EAP version.
    return parsedJson.find { it -> it.code == product }.releases.find { it -> it.type == "eap" }.build
}

static def getSinceBuild(String product, String since) {
    // Get the list of all versions.
    def versionUrl = new URI("https://data.services.jetbrains.com/products?code=" + product + "&fields=code%2Creleases.build%2Creleases.type%2Creleases.version").toURL()
    def parsedJson = new JsonSlurper().parse(versionUrl)

    // Find the latest version of the given major.
    final String sinceMajor = since.split("\\.")[0]
    return parsedJson.find { it -> it.code == product }.releases.find { it -> it.type == "release" && it.build.split("\\.")[0] == sinceMajor }.version
}