import groovy.json.JsonSlurper
import org.jetbrains.intellij.tasks.RunPluginVerifierTask

plugins {
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'org.jetbrains.intellij' version '1.16.0'
}

group 'io.github.thepieterdc.dodona'
version '2.1.10'

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()

    maven {
        url = "https://maven.pkg.github.com/thepieterdc/dodona-api-java"
        credentials {
            username = "PublicToken"
            password = "\u0064\u0064\u0066\u0039\u0036\u0065\u0030\u0063\u0061\u0032\u0066\u0065\u0032\u0039\u0033\u0033\u0036\u0030\u0035\u0062\u0031\u0033\u0031\u0035\u0038\u0063\u0061\u0064\u0061\u0033\u0030\u0031\u0030\u0035\u0062\u0037\u0035\u0032\u0066\u0061"
        }
    }
}

dependencies {
    implementation group: 'io.github.thepieterdc.dodona', name: 'api', version: '2.0.3'
    implementation group: 'io.github.thepieterdc.dodona', name: 'impl', version: '2.0.3'

    implementation group: 'com.github.marlonlom', name: 'timeago', version: '4.0.3'

    testImplementation platform("org.junit:junit-bom:5.10.0")

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.6.0'
    testImplementation group: 'io.github.thepieterdc.random', name: 'random', version: '1.0.2'
}

task calculateNextVersion {
    doLast {
        def version = (project.version as String).split('\\.').collect { Integer.parseInt(it) }
        println(String.format("%s.%s.%s", version[0], version[1], version[2] + 1))
    }
}

task generateBuildConfig {
    var outputDir = file("$projectDir/src/main/java/")
    doFirst {
        def srcFile = new File(outputDir, "io/github/thepieterdc/dodona/plugin/BuildConfig.java")
        srcFile.parentFile.mkdirs()
        srcFile.write("""
package io.github.thepieterdc.dodona.plugin;
public class BuildConfig {
    public static final String VERSION = "$project.version";
}
""")
    }

    group = "build"
    outputs.upToDateWhen { false }
}

compileJava.dependsOn generateBuildConfig
prepareKotlinBuildScriptModel.dependsOn generateBuildConfig

static def getVerifiableVersions() {
    // FIXME: Re-enable PyCharm checks.
    def versions = ["IC-211.7628.21", /*"PC-203.6682.168" */]

    // Get the list of all versions.
    def versionUrl = new URL("https://data.services.jetbrains.com/products?code=IIC%2CPCC&fields=code%2Creleases.build%2Creleases.type")
    def parsedJson = new JsonSlurper().parse(versionUrl)

    // Find the latest EAP IntelliJ version.
    versions.add(String.format("IC-%s", parsedJson.find { it -> it.code == "IIC" }.releases.find { it -> it.type == "eap" }.build))

    // Find the latest EAP PyCharm version.
    // FIXME: Re-enable PyCharm checks.
//    versions.add(String.format("PC-%s", parsedJson.find { it -> it.code == "PCC" }.releases.find { it -> it.type == "eap" }.build))
    return versions
}

intellij {
    downloadSources.set(false)
    pluginName.set('dodona')
    plugins.set(['java', 'PythonCore:211.7628.24'])
    updateSinceUntilBuild.set(false)
    version.set('211.7628.21')
}

jacocoTestReport {
    dependsOn test

    reports {
        html.required = true
        xml.required = true
    }
}

patchPluginXml {
    changeNotes = """
      <ul>
        <li>[BUG] Show the submit icon in the new layout.</li>
      </ul>
      """

    pluginDescription = 'Companion plugin for the Ghent University Dodona platform, which allows you to submit exercises right from your favourite JetBrains IDE. More information can be found at <a href="https://docs.dodona.be/en/guides/pycharm-plugin/">https://docs.dodona.be/en/guides/pycharm-plugin/</a>'

    sinceBuild = '211.7628.21'
}

publishPlugin {
    channels = [(project.version as String).contains("SNAPSHOT") ? "nightly" : "stable"]
    token = System.getenv("JETBRAINS_MARKETPLACE_TOKEN")
}

runPluginVerifier {
    failureLevel = [RunPluginVerifierTask.FailureLevel.COMPATIBILITY_PROBLEMS]
    ideVersions = getVerifiableVersions()
}

wrapper {
    gradleVersion = '8.2.1'
}
