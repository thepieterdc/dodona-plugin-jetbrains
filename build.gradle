plugins {
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.3'
}

group 'io.github.thepieterdc.dodona'
version '2.1.3-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenCentral()

    maven {
        url = "https://maven.pkg.github.com/thepieterdc/dodona-api-java"
        credentials {
            username = "PublicToken"
            password = "\u0064\u0064\u0066\u0039\u0036\u0065\u0030\u0063\u0061\u0032\u0066\u0065\u0032\u0039\u0033\u0033\u0036\u0030\u0035\u0062\u0031\u0033\u0031\u0035\u0038\u0063\u0061\u0064\u0061\u0033\u0030\u0031\u0030\u0035\u0062\u0037\u0035\u0032\u0066\u0061"
        }
    }

    maven {
        url "https://dl.bintray.com/thepieterdc/random-java"
    }
}

dependencies {
    implementation group: 'io.github.thepieterdc.dodona', name: 'dodona-api-interface', version: '1.9.3'
    implementation group: 'io.github.thepieterdc.dodona', name: 'dodona-api-impl', version: '1.9.3'

    implementation group: 'com.github.marlonlom', name: 'timeago', version: '4.0.3'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.0'
    testImplementation group: 'io.github.thepieterdc.random', name: 'random', version: '0.3.0'
}

task generateBuildConfig {
    var outputDir = file("$projectDir/src/main/java/")
    doFirst {
        def srcFile = new File(outputDir, "io/github/thepieterdc/dodona/plugin/BuildConfig.java")
        srcFile.parentFile.mkdirs()
        srcFile.write("""
package io.github.thepieterdc.dodona.plugin;
public class BuildConfig {
    public static final String VERSION = "$project.version";
}
""")
    }
}
compileJava.dependsOn generateBuildConfig

intellij {
    downloadSources false
    pluginName 'dodona'
    plugins = ['java', 'PythonCore:193.5233.102']
    updateSinceUntilBuild false
    version '193.5233.102'
}

jacocoTestReport {
    dependsOn test

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

patchPluginXml {
    changeNotes """
      <ul>
        <li></li>
      </ul>
      """

    pluginDescription "Companion plugin for the Ghent University Dodona platform, which allows you to submit exercises right from your favourite JetBrains IDE."

    sinceBuild '193.3519.25'
}

publishPlugin {
    channels 'nightly'
    token System.getenv("JETBRAINS_MARKETPLACE_TOKEN")
}

wrapper {
    gradleVersion = '7.0.0'
}
